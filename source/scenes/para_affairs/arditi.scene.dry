title: Arditi del Popolo
new-page: true
is-card: true
on-arrival: arditi_timer = 2; week_actions += 1; 
view-if: arditi_formed and arditi_timer <= 0 and arditi_cooperation
frequency: 300
card-image: img/Arditi.png
tags: para_affairs

= Arditi del Popolo

The *Arditi del Popolo* is a broad anti-fascist group, formed to combat the *Fasci Italiani di Combattimento* comprised of the broad left, ranging from Communists, Syndicalists, Anarchists, Socialists, Trade Unionists to Republicans and everything in between. Though some on the Left-wing of the party aren't entirely enthused about our decision to support the Arditi del Popolo due to their broad stance, it seems the growing rise of fascism has forced our hand.

Though its broad reach has allowed it to remain non-sectarian to any party, they would certainly be willing to cooperate with us or welcome any funds we are to provide to them, being a valuable ally in the event the fascists attempt anything. What should we do?

- @arditi_funding: Fund the Arditi del Popolo.
- @arditi_training: Utilise the experienced Arditi members to train the Red Guards.
- @arditi_fasci: Co-ordinate raids against the far-right groups.
- @easy_discard
- @cancel_advisor_action

@arditi_funding 
subtitle: -1 Paramilitary Resource
choose-if: para_resources > 0
unavailable-subtitle: Not enough paramilitary resources. (Need 1)
on-arrival: para_resources -= 1; adp_strength += 17; adp_militancy += 0.05; socialist_sentiment += 1; republican_sentiment += 3;

Our funding allows the Arditi to grow in number, strengthening our relations with them in the process.

@arditi_training
on-arrival: rg_strength += adp_strength*0.07 if adp_strength < 100; rg_strength += adp_strength*0.02 if adp_strength >= 100; rg_militancy += adp_militancy*0.1; arditi_rg_cooperation = 1

The more experienced Arditi offer to help train the Red Guards, growing our strength and making our members more proficient.

@arditi_fasci
on-arrival: adp_strength -= 17; adp_militancy += 0.02*(1-dissent); rg_militancy += 0.02*(1-dissent); fdc_militancy -= 0.03*(1-dissent); fdc_strength += 28; ard_militancy -= 0.01*(1*dissent); ard_strength += 12; abl_strength += 9; radicalisation += 0.05; communist_strength += 7*(1-dissent); communist_dissent -= 5*(1-dissent); nationalist_sentiment -= 2; middle_class_psi -= 3*(1-dissent); rural_psi += 1*(1-dissent); strife -= 0.005;

The Arditi, under our guidance, are able to carry out raids against far-right forces, seizing weapons and equipment. Our move secures weaponry for the Arditi and our own Red Guards, though the right take the opportunity to paint us as aggressive radicals to their backers, securing funding.